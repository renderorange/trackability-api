#!/usr/bin/env perl

use strict;
use warnings;

use FindBin ();
use lib "$FindBin::RealBin/../lib";
use Trackability::API::Model::Users;

use Getopt::Long  ();
use Pod::Usage    ();
use Try::Tiny;

my $VERSION = '0.001';

my %opt = ();
Getopt::Long::GetOptions(
    \%opt,
    'id=i',
    'version' => sub { print "$0 version $VERSION\n"; exit 0 },
    'help|?',
) or Pod::Usage::pod2usage( -exitval => 1 );

Pod::Usage::pod2usage( -exitval => 0, -verbose => 1 ) if $opt{help};
Pod::Usage::pod2usage( -exitval => 1, -verbose => 0 ) unless $opt{id};

my $users = Trackability::API::Model::Users->get( id => $opt{id} );
die "$0: user " . $opt{id} . " doesn't exist\n"
    unless $users->[0];

my $key = try {
    return $users->[0]->add_key();
}
catch {
    die "$0: " . $_ . "\n";
};

die "$0: add key failed\n"
    unless $key;

print "key: $key\n";

exit 0;

__END__

=pod

=head1 NAME

add-key - add an API key for a user

=head1 SYNOPSIS

 add-key [--id <id>] [--help]

=head1 DESCRIPTION

C<add-key> is a program to add a key to the database for a user.

=head1 OPTIONS

=over

=item --id

The user's id.

=item --version

Print the version of this program.

=item --help

Print the help menu.

=back

=head1 AUTHOR

Blaine Motsinger C<blaine@renderorange.com>

=cut
