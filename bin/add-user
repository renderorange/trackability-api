#!/usr/bin/env perl

use strict;
use warnings;

use FindBin ();
use lib "$FindBin::RealBin/../lib";
use Trackability::API::Model::Users;

use Getopt::Long ();
use Pod::Usage   ();
use Try::Tiny;

my $VERSION = '0.001';

my %opt = ();
Getopt::Long::GetOptions(
    \%opt,
    'name=s',
    'email=s',
    'version' => sub { print "$0 version $VERSION\n"; exit 0 },
    'help|?',
) or Pod::Usage::pod2usage( -exitval => 1 );

Pod::Usage::pod2usage( -exitval => 0, -verbose => 1 ) if $opt{help};
Pod::Usage::pod2usage( -exitval => 1, -verbose => 0 ) unless $opt{name} and $opt{email};

die "$0: user " . $opt{email} . " already exists\n"
    if Trackability::API::Model::Users->get( email => $opt{email} );

my $user = Trackability::API::Model::Users->new(
    name  => $opt{name},
    email => $opt{email},
);

try {
    $user->store();
}
catch {
    my $exception = $_;
    die "$0: " . $exception->message . "\n";
};

print "user $user->{id} created\n";

exit 0;

__END__

=pod

=head1 NAME

add-user - add a user to the database

=head1 SYNOPSIS

 add-user [--name <name>] [--email <email>] [--help]

=head1 DESCRIPTION

C<add-user> is a program to add a user to the database.

=head1 OPTIONS

=over

=item --name

The user's name.

=item --email

The user's email.

=item --version

Print the version of this program.

=item --help

Print the help menu.

=back

=head1 AUTHOR

Blaine Motsinger C<blaine@renderorange.com>

=cut
